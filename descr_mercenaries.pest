file = _{ SOI ~ NEWLINE* ~ pool+ ~ EOI }

pool = {
    id_decl ~ NEWLINE+
  ~ regions ~ NEWLINE+
  ~ (units ~ NEWLINE+)+
}
id_decl = _{ "pool" ~ id }
id = @{ ident }
regions = { "regions" ~ region_list }
region_list = _{ region+ }
region = @{ ident }
units = {
    unit_id ~ ","
  ~ unit_exp
  ~ unit_cost
  ~ unit_replenish
  ~ unit_max
  ~ unit_initial
  ~ unit_restrict?
}
unit_id = _{ "unit" ~ unit }
unit = @{ !" " ~ (ASCII_ALPHANUMERIC | "_" | "-" | " ")+ }
unit_exp = _{ "exp" ~ exp }
exp = @{ num }
unit_cost = _{ "cost" ~ cost }
cost = @{ num }
unit_replenish = _{ "replenish" ~ replenish }
replenish = { max ~ "-" ~ min }
max = @{ float }
min = @{ float }
unit_max = _{ "max" ~ max }
unit_initial = _{ "initial" ~ initial }
initial = @{ num }
unit_restrict = { "restrict" ~ faction_list }
faction_list = _{ ident ~ ("," ~ ident)* }

num = @{ ASCII_DIGIT+ }
float = @{ num ~ ("." ~ num)? }
ident = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }

COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
WHITESPACE = _{ " " | "\t" }
