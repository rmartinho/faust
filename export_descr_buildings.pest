file = _{
	SOI 
  ~ tags_decl?
  ~ alias_decl*
  ~ hidden_resources_decl?
  ~ buildings
  ~ EOI
}

tags_decl = _{ "tags" ~ "{" ~ tags ~ "}" }
tags = { tag+ }
tag = @{ ident }
alias_decl = {
	"alias" ~ alias ~ "{"
  ~ requires_decl
  ~ display_string_decl?
  ~ "}"
}
alias = @{ ident }
requires_decl = _{ "requires" ~ requires }
display_string_decl = _{ "display_string" ~ display_string }
display_string = @{ ident }
hidden_resources_decl = _{ "hidden_resources" ~ hidden_resources }
hidden_resources = ${ (resource ~ (!NEWLINE ~ WHITESPACE*))* ~ resource* }
resource = @{ ident }

buildings = { building+ }
building = {
	"building" ~ id ~ "{"
  ~ ( icon_decl
    | classification_decl
    | tag_decl
    | levels_decl
    | plugins_decl
    )*
 ~ "}" }
id = @{ ident }
icon_decl = _{ "icon" ~ icon }
icon = @{ ident }
classification_decl = _{ "classification" ~ classification }
classification = @{ ident }
tag_decl = _{ "tag" ~ tag }
levels_decl = _{ "levels" ~ (!"{" ~ ANY)+ ~ "{" ~ level+ ~ "}" }
level = {
	id ~ requires_decl? ~ "{"
  ~ ( capabilities_decl
    | construction_decl
    | cost_decl
    | settlement_min_decl
    | upgrades_decl
    | ai_destruction_hint_decl
    | faction_capablities_decl
    )*
~ "}" }
capabilities_decl = _{ "capability" ~ "{" ~ capabilities ~ "}" }
capabilities = { capability+ }
faction_capablities_decl = _{ "faction_capability" ~ "{" ~ capabilities ~ "}" }
construction_decl = _{ "construction" ~ constructione }
constructione = @{ num }
cost_decl = _{ "cost" ~ cost }
cost = @{ num }
settlement_min_decl = _{ "settlement_min" ~ settlement_min }
settlement_min = @{ ident }
upgrades_decl = _{ "upgrades" ~ "{" ~ upgrades? ~ "}" }
upgrades = { id+ }
ai_destruction_hint_decl = _{ "ai_destruction_hint" ~ ai_destruction_hint }
ai_destruction_hint = ${ requires }
plugins_decl = _{ "plugins" ~ "{" ~ plugins? ~ "}" }
plugins = { plugin+ }
plugin = @{ (!"}" ~ !NEWLINE ~ ANY)+ }

capability = {
    recruit
  | unknown_cap
}
unknown_cap = @{ ident ~ (!NEWLINE ~ ANY)+ }
recruit = { "recruit" ~ "\"" ~ unit ~ "\"" ~ exp ~ requires_decl? }
unit = @{ string }
exp = @{ num }

requires = {
  | req_or
}
req_primary = _{
	req_resource
  | req_hidden_resource
  | req_event
  | req_factions
  | req_building_factions
  | req_is_toggled
  | req_building_present_min_level
  | req_building_present
  | req_religion
  | req_no_tag
  | req_unknown
}
req_unknown = @{ (!NEWLINE ~ ANY)+ }
req_or = _{ or | req_and }
req_and = _{ and | req_not }
and = { req_not ~ ("and" ~ req_not)+ }
or = { req_and ~ ("or" ~ req_and)+ }
req_not = _{ not | req_primary }
not = { "not" ~ req_primary }
req_resource = _{ "resource" ~ resource }
req_hidden_resource = _{ "hidden_resource" ~ hidden_resource }
hidden_resource = @{ ident }
req_event = _{ "major_event" ~ "\"" ~ event ~ "\"" }
event = @{ string }
req_factions = _{ "factions" ~ "{" ~ factions ~ "}" }
factions = { (faction ~ ","+)+ }
faction = @{ ident }
req_building_factions = _{ "building_factions" ~ "{" ~ building_factions ~ "}" }
building_factions = { (faction ~ ","+)+ }
req_is_toggled = _{ is_toggled }
is_toggled = { "is_toggled" ~ "\"" ~ toggle ~ "\"" }
toggle = @{ string }
req_building_present = _{ "building_present" ~ building_present } 
building_present = @{ ident }
req_building_present_min_level = _{ "building_present_min_level" ~ type ~ min_level } 
building_level = { type ~ min_level }
type = @{ ident }
min_level = @{ ident }
req_religion = _{ "religion" ~ religion_cond }
religion_cond = { religion ~ sign ~ amount }
religion = @{ ident }
sign = @{ "<" | ">" | "<=" | ">=" }
amount = @{ num }
req_no_tag = _{ "no_building_tagged" ~ no_tag }
no_tag = { tag ~ queued? }
queued = @{ "queued"}

ident = @{ (ASCII_ALPHANUMERIC | "_" | "-" | "+")+ }
string = @{ (!"\"" ~ ANY)* }
num = @{ ASCII_DIGIT+ }

COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
WHITESPACE = _{ " " | "\t" | NEWLINE }
